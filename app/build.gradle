apply plugin: 'com.android.application'
apply plugin: 'com.github.wangjiegulu.plg.rapidmetainf'

rapidmetainf {
    metaInfName 'DAL_REQUEST_DEMO.properties'

    String[] infArray = new String[10]
    // ./gradlew clean build -PcommandKey=commandValue
    infArray[0] = "propertyFromCommand=${getParameter('commandKey')}"
    for(int i = 1; i < infArray.length; i++){
        infArray[i] = "array_item_key_$i=array_item_value_$i"
    }

    metaInfProperties infArray
}

def getParameter(String key) {
    // -D
    String value = System.getProperty(key)
    if (null != value && value.length() > 0) {
        return value
    }
    // -P
    if (hasProperty(key)) {
        return getProperty(key)
    }
    return null
}

android {
    compileSdkVersion global_compileSdkVersion
    defaultConfig {
        applicationId "com.wangjie.dal.request.sample"
        minSdkVersion global_minSdkVersion
        targetSdkVersion global_targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$global_supportVersion"
    implementation "com.android.support.constraint:constraint-layout:$global_constraintLayoutVersion"

    // rxjava/rxandroid
    implementation "io.reactivex.rxjava2:rxjava:$global_rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$global_rxandroidVersion"

    // gson
    implementation "com.google.code.gson:gson:$global_gsonVersion"

    implementation project(':library')
//    implementation "com.github.wangjiegulu:dal_request:1.0.2-SNAPSHOT"
//    implementation "com.github.wangjiegulu:dal_request-db:0.0.1-SNAPSHOT"

    testImplementation "junit:junit:$global_junitVersion"
    androidTestImplementation "com.android.support.test:runner:$global_runnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$global_espressoVersion"
}
